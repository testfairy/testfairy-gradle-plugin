apply plugin: 'groovy'

dependencies {
	compile gradleApi()
	compile localGroovy()
	compile 'com.testfairy:testfairy-uploader:1.1.1'
	compile 'com.android.tools.build:gradle:0.8.0'
}

apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.testfairy'
version = (project.hasProperty('revision') ? project.property('revision') : '2.0.0')

repositories {
	mavenCentral()
	jcenter()
}

configurations {
	includeInJa
}

compileJava {
	sourceCompatibility = 1.6
	targetCompatibility = 1.6
}

signing {
	required{ isRelease() }
	sign configurations.archives
}

def getRepositoryUsername() {
	return hasProperty('sonatypeUsername') ? sonatypeUsername : System.getenv("SONATYPE_USERNAME")
}

def getRepositoryPassword() {
	return hasProperty('sonatypePassword') ? sonatypePassword : System.getenv("SONATYPE_PASSWORD")
}

def isRelease() {
	return hasProperty('isRelease') ? isRelease == 'true' : false;
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			pom.groupId = project.group
			pom.artifactId = 'testfairy-gradle-plugin'
			pom.version = project.version
			println pom.version

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
			}

			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
			}

			pom.project {
				name 'TestFairy Uploader Plugin for Gradle'
				description 'Upload signed builds directly to TestFairy platform.'
				artifactId 'testfairy-gradle-plugin'
				url 'https://www.testfairy.com'
				inceptionYear '2013'

				licenses {
					license {
						name 'The Apache Software License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution 'repo'
					}
				}

				scm {
					url "https://github.com/testfairy/testfairy-gradle-plugin"
					connection 'scm:git:https://github.com/testfairy/testfairy-gradle-plugin/'
					developerConnection 'scm:git:https://github.com/testfairy/testfairy-gradle-plugin/'
				}

				developers {
					developer {
						name 'Gil Megidish'
						email 'gil@testfairy.com'
						organization 'TestFairy'
						organizationUrl 'https://www.testfairy.com'
					}
				}
			}
		}
	}
}

jar {
}
